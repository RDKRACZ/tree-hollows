plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT' apply false
    id 'maven-publish'
}

var accessWidenerFile = file("core/src/main/resources/tree-hollows.accesswidener")

allprojects {
    apply plugin: "fabric-loom"

    version = project.mod_version
    group = project.maven_group

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        // for ModMenu
        maven {
            url = "https://maven.terraformersmc.com/releases"
        }
        // used for ClothConfig API, which is dependency of Architect's Palette
        maven { url "https://maven.shedaniel.me/" }
        // used to get Architect's Palette
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
        modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    }

    loom {
        accessWidenerPath = accessWidenerFile
        shareCaches()
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    java {
        withSourcesJar()
    }
}

dependencies {
    implementation project(path: ":core", configuration: "namedElements")
    include project(path: ":core")

    implementation project(path: ":architects-palette-support", configuration: "namedElements")
    include project(path: ":architects-palette-support")

    modRuntimeOnly  "curse.maven:architects-palette-487364:3695654"
    modRuntimeOnly ("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
        transitive = false
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}