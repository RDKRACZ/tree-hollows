plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT' apply false
    id 'maven-publish'
}

var accessWidenerFile = file("main/src/main/resources/tree-hollows.accesswidener")

allprojects {
    apply plugin: "fabric-loom"

    version = project.mod_version
    group = project.maven_group

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        // for ModMenu
        maven {
            url = "https://maven.terraformersmc.com/releases"
        }
        maven {
            url "https://maven.parchmentmc.net/"
            content {
                includeGroup "org.parchmentmc.data"
            }
        }
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.19.2:2022.10.09@zip")
        }
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
        modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    }

    loom {
        accessWidenerPath = accessWidenerFile
        shareCaches()
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    java {
        withSourcesJar()
    }
}

dependencies {
    implementation project(path: ":main", configuration: "namedElements")
    include project(path: ":main")

//    implementation project(path: ":architects-palette-support", configuration: "namedElements")
//    include project(path: ":architects-palette-support")

//    modRuntimeOnly  "curse.maven:architects-palette-487364:3695654"
//    modRuntimeOnly ("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
//        transitive = false
//    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}